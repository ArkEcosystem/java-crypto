plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'com.diffplug.spotless' version '5.6.1'
}

group = 'org.arkecosystem'
version = '2.0.0-beta-1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation group: 'org.bitcoinj', name: 'bitcoinj-core', version: '0.15.8'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation 'com.google.guava:guava:30.1.1-jre'

    testCompileOnly 'org.slf4j:slf4j-api:1.7.32'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.32'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testCompileOnly 'org.junit.jupiter:junit-jupiter-params:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

spotless {
    java {
        target fileTree(projectDir) {
            include 'src/main/**/*.java'
            include 'src/test/**/*.java'
            exclude '**/build/**'
        }
        googleJavaFormat('1.1').aosp()
    }
}

task formatCode(dependsOn: ['spotlessApply'])

task fatJar(type: Jar) {
    manifest.from jar.manifest
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

build.dependsOn 'spotlessApply'

wrapper {
    gradleVersion = '7.2'
}

task writeNewPom {
    doLast {
        pom {
            groupId = project.group
            version = project.version
            artifactId = 'crypto'

            name = 'java-crypto'
            description = 'A Simple Cryptography Implementation in Java for the ARK CORE Blockchain Framework.'
            url = 'https://sdk.ark.dev/java/crypto'
            inceptionYear = '2018'

            licenses {
                license {
                    name = 'MIT'
                    distribution = 'repo'
                }
            }

            developers {
                developer {
                    name = 'Kovač Žan'
                    email = 'zan@ark.io'
                    organization = 'ARK Ecosystem'
                    organizationUrl = 'https://ark.io'
                }
                developer {
                    name = 'Kristjan Košič'
                    email = 'kristjan@ark.io'
                    organization = 'ARK Ecosystem'
                    organizationUrl = 'https://ark.io'
                }
                developer {
                    name = 'Brian Faust'
                    email = 'brian@ark.io'
                    organization = 'ARK Ecosystem'
                    organizationUrl = 'https://ark.io'
                }
                developer {
                    name = 'Joshua Noack'
                    email = 'joshua@ark.io'
                    organization = 'ARK Ecosystem'
                    organizationUrl = 'https://ark.io'
                }
            }

            scm {
                connection = 'scm:git:git://github.com/ArkEcosystem/java-crypto.git'
                developerConnection = 'scm:git:ssh://github.com:ArkEcosystem/java-crypto.git'
                url = 'https://github.com/ArkEcosystem/java-crypto'
            }
        }.writeTo("$buildDir/newpom.xml")
    }
}

publishing {

    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/content/groups/public/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
