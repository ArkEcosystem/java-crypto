plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

apply plugin:'java'
apply plugin:'maven'
apply plugin:'maven-publish'
apply plugin:'signing'

repositories {
    jcenter()
    mavenCentral()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        arkEcosystem(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            pom {
                groupId = 'org.arkecosystem.crypto'
                version = '0.1.0'
                artifactId = 'arkecosystem-crypto'

                name = 'java-crypto'
                description = 'A simple Cryptography Implementation in Java for the ARK Blockchain.'
                url = 'https://github.com/ArkEcosystem/java-crypto'
                inceptionYear = '2018'

                licenses {
                    license {
                        name = 'MIT'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        name = 'Brian Faust'
                        email = 'brian@ark.io'
                        organization = 'Ark Ecosystem'
                        organizationUrl = 'https://ark.io'
                    }
                    developer {
                        name = 'Joshua Noack'
                        email = 'joshua@ark.io'
                        organization = 'Ark Ecosystem'
                        organizationUrl = 'https://ark.io'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/ArkEcosystem/java-crypto.git'
                    developerConnection = 'scm:git:ssh://github.com:ArkEcosystem/java-crypto.git'
                    url = 'https://github.com/ArkEcosystem/java-crypto/tree/master'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'myRepo'
            url = "file://${buildDir}/repo"
        }
    }
}

signing {
    sign publishing.publications.arkEcosystem
}

dependencies {
    compile group: 'org.bitcoinj', name: 'bitcoinj-core', version: '0.14.7'
    compile group: 'com.madgag.spongycastle', name: 'core', version: '1.54.0.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
    failFast = true
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}
